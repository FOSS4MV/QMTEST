$catalog AnnotateTests global
program AnnotateTests

    sourceFileName = @SENTENCE[' ', 2, 1]
    sourceItemName = @SENTENCE[' ', 3, 1]

    open sourceFileName to sourceFile else stop 201, sourceFileName
    read source from sourceFile, sourceItemName else stop 202, sourceItemName
    original = source
    
    sourceLines = dcount(source, @fm)
    for l = 1 to sourceLines
        line = source<l>
        utline = upcase(trim(line))
        begin case
            case (utline[1, 5] = "*TEST")
                spcs = line['*', 1, 1]
                anno = line['*', 2, *]
                line2 = trim(source<l + 1>)
                if (upcase(line2) matches "'PUBLIC'0Xý'PRIVATE'0Xý'LOCAL'0X") then
                    method = line2[' ', 3, 1]['(', 1, 1]
                end else
                    method = line2[' ', 2, 1]['(', 1, 1] : ":" : line2[' ', 1, 1]
                end
                line = spcs : "$* @" : anno : ':' : method
                source<l> = line
            case (utline matches "'*AFTER'0Xý'*BEFORE'0X")
                spcs = line['*', 1, 1]
                anno = line['*', 2, *]
                line2 = trim(source<l + 1>)
                method = line2[' ', 3, 1]['(', 1, 1]
                line = spcs : "$* @" : anno : ':' : method
                source<l> = line
            case (utline matches "'*IGNORE'0Xý'*SKIP'0X")
                spcs = line['*', 1, 1]
                anno = line['*', 2, *]
                line = spcs : "$* @" : anno
                source<l> = line
        end case
    next l
    write source on sourceFile, sourceItemName
    
    cmd = "basic " : sourceFileName : " " : sourceItemName
    execute cmd capturing output
    
    write original on sourceFile, sourceItemName

end
